     A                                      INDARA
     A                                      CA03(03)
     A          R HEADER                    OVERLAY
     A                                  1 27'Student Information System'
     A                                      COLOR(WHT)
     A                                  1 71TIME
     A                                      COLOR(BLU)
     A                                  1  3DATE(*SYS *YY)
     A                                      COLOR(BLU)
     A          R FOOTER_M                  OVERLAY
     A                                 23 22'F3-Exit'
     A                                      COLOR(RED)
     A                                 23 51'F5-REFRESH'
     A                                      COLOR(BLU)
     A          R FOOTER_ADD                OVERLAY
     A                                 23 10'F3-EXIT'
     A                                      COLOR(RED)
     A                                 23 26'F5-REFRESH'
     A                                      COLOR(BLU)
     A                                 23 45'F8-ADD'
     A                                 23 61'F12-CANCEL'
     A                                      COLOR(BLU)
     A          R FOOTER_UP                 OVERLAY
     A                                 23  6'F3-EXIT'
     A                                      COLOR(RED)
     A                                 23 19'F5-REFRESH'
     A                                      COLOR(BLU)
     A                                 23 35'F8-UPDATE'
     A                                 23 50'F11-FOLD/DROP'
     A                                 23 68'F12-CANCEL'
     A                                      COLOR(BLU)
     A          R LOGIN                     CF05(05)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                  5 28'Welcome:'
     A                                  5 37USER
     A                                  9 22'User Name...:'
     A                                 10 22'Password....:'
     A            MSG_LOGIN     50   O 14 16COLOR(YLW)
     A            USRNAME       10   B  9 36CHECK(LC)
     A            USRPWD        10   B 10 36CHECK(LC)
     A                                      DSPATR(ND)
     A                                  7 25'Please log in to continue'
     A          R MAINMENU                  CF05(05)
     A                                      OVERLAY
     A                                  5  8'1-Select'
     A                                  7 23'Add New Subject'
     A                                  9 23'Add New Course'
     A                                 11 23'Add New Student'
     A                                 13 23'View/Add Student Details'
     A            OPT_NEWSUB     1M  I  7 21
     A            OPT_NEWCRS     1M  I  9 21
     A            OPT_NEWSTU     1M  I 11 21
     A            OPT_VIEWST     1M  I 13 21
     A            MSG_MENU      50   O 16 15COLOR(TRQ)
     A          R NEWSUBJECT                CF08(08)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                  5  6'Add New Subject'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  9 20'Subject Name:'
     A            NEWSUBNAME    25   B  9 34CHECK(LC)
     A            MSG_NEWSUB    50   O 13 17COLOR(TRQ)
     A          R MSGREC                    OVERLAY
     A            MESSAGE       60   O 22 10COLOR(TRQ)
     A          R SUBSFL                    SFL
     A            SUB_ID         5N 0O 13 25
     A            SUB_NAME      25   O 13 43
     A            OPT_SUB        1M  I 13  9
     A          R SUBCTL                    SFLCTL(SUBSFL)
     A  50                                  SFLDSPCTL
     A  50                                  SFLDSP
     A N50                                  SFLCLR
     A  50                                  SFLEND(*MORE)
     A                                      OVERLAY
     A                                      SFLPAG(6)
     A                                      SFLSIZ(1000)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      CF08(08)
     A                                 12  8'OPT'
     A                                 12 23'Subject ID'
     A                                 12 49'Subject Name'
     A                                  4  4'Add New Course'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  6 23'Course....:'
     A            CRSNAME       25   B  6 35CHECK(LC)
     A                                  8 23'Semester..:'
     A            SEMESTER      10   B  8 35CHECK(LC)
     A                                 11  5'1-Select'
     A            MSG_NEWCRS    70   B 10  6COLOR(TRQ)
     A          R CRSSFL                    SFL
     A  60                                  SFLNXTCHG
     A            OPT_CRS        1M  I 13  9
     A            CRS_NAME      25   O 13 43
     A            CRS_ID         5N 0O 13 25
     A          R CRSCTL                    SFLCTL(CRSSFL)
     A  55                                  SFLDSPCTL
     A  55                                  SFLDSP
     A N55                                  SFLCLR
     A  55                                  SFLEND(*MORE)
     A                                      OVERLAY
     A                                      SFLPAG(6)
     A                                      SFLSIZ(1000)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      CF08(08)
     A                                 12  8'OPT'
     A                                 12 23'Course ID'
     A                                 12 50'Course Name'
     A                                  4  4'Add New Student'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  6 21'Student Name...:'
     A            NSTUDNAME     25   B  6 39CHECK(LC)
     A                                  8 18'Student Address:..:'
     A            NSTUDADDR     25   B  8 39CHECK(LC)
     A                                 11  5'1-Select'
     A            MSG_NEWSTU    70   B 10  6COLOR(TRQ)
     A          R STUDSFL                   SFL
     A            OPT_STUD       1M  B 11  3
     A            ST_ID          5N 0O 11  5
     A            ST_NAME       25   O 11 12
     A            ST_CRSID       5N 0O 11 38
     A            ST_SEMEST     10   O 11 45
     A            ST_SUBID       5N 0O 11 60
     A            ST_MARKS       5N 0B 11 71
     A            ST_ADDR       25   O 13  3
     A                                 12 12'Address'
     A                                      COLOR(WHT)
     A                                 12 39'Subject'
     A                                      COLOR(WHT)
     A                                 12 62'Course Name'
     A                                      COLOR(WHT)
     A            ST_SUBJECT    25   O 13 29
     A            ST_CRSNAME    25   O 13 55
     A                                 14  7'\\---------------------------------
     A                                      ------------------------------------
     A                                      //'
     A                                      COLOR(WHT)
     A          R STUDCTL                   SFLCTL(STUDSFL)
     A  50                                  SFLDSPCTL
     A  51                                  SFLDSP
     A N50                                  SFLCLR
     A  50                                  SFLEND(*MORE)
     A                                      OVERLAY
     A                                      SFLPAG(3)
     A                                      SFLSIZ(1000)
     A                                 10  2'Opt'
     A                                      COLOR(WHT)
     A                                 10  6'ID'
     A                                      COLOR(WHT)
     A                                 10 19'Student Name'
     A                                      COLOR(WHT)
     A                                 10 37'Crs ID'
     A                                      COLOR(WHT)
     A                                 10 46'Semester'
     A                                      COLOR(WHT)
     A                                 10 57'Subject ID'
     A                                      COLOR(WHT)
     A                                 10 71'Marks'
     A                                      COLOR(WHT)
     A                                  3  7'Student ID:'
     A            ST_INPID       5N 0B  3 19
     A                                  4  7'Course ID:'
     A            ST_INPCRS      5N 0B  4 19
     A                                  5  7'Semester:'
     A            ST_INPSEM     10   B  5 19
     A                                  3 33'OR'
     A                                  4 33'OR'
     A                                  3 38'Student Name:'
     A            ST_INPNAM     25   B  3 52
     A                                  4 38'Course Name:'
     A            ST_INPCRS1    25   B  4 52
     A                                  9  2'A-Add New Marks'
     A                                      COLOR(BLU)
     A                                  9 21'C-Change Semester'
     A                                      COLOR(BLU)
     A                                  9 42'D-Delete'
     A                                      COLOR(BLU)
     A            MSG_STUDET    70   B  7  6COLOR(TRQ)
     A          R ADDDETAIL                 OVERLAY
     A                                  4  6'Add Details'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  6 19'Student ID....:'
     A                                  8 19'Student Name..:'
     A                                 10 19'Course........:'
     A                                 12 19'Semester......:'
     A                                 14 19'Subject.......:'
     A                                 16 19'Marks.........:'
     A            DT_STID        5N 0O  6 37
     A            DT_STNAME     25   O  8 37
     A            DT_CRSNAME    25   O 10 37
     A            DT_SEMEST     10   O 12 37
     A            DT_SUBNAME    25   B 14 37
     A            DT_MARKS       5N 0B 16 37
     A          R CHGSMST                   OVERLAY
     A                                  4  6'Change Semester'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  6 19'Student ID....:'
     A                                  8 19'Student Name..:'
     A                                 10 19'Course........:'
     A            CS_CRSNAM     25     10 36
     A            CS_STNAME     25      8 36
     A            CS_STID        5N 0   6 36
     A                                 12 19'Semester......:'
     A            CS_SEMEST     10   B 12 36

