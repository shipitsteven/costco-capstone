     A                                      INDARA
     A                                      CA03(03)
     A          R HEADER                    OVERLAY
     A                                  1 27'Student Information System'
     A                                      COLOR(WHT)
     A                                  1  3DATE(*SYS *YY)
     A                                      COLOR(BLU)
     A                                      EDTWRD('  /  /    ')
     A            CURUSER       10   O  1 71
     A                                  1 64'Hello,'
     A                                      COLOR(BLU)
     A          R FOOTER_M                  OVERLAY
     A                                 23 22'F3-Exit'
     A                                      COLOR(BLU)
     A                                 23 51'F5-REFRESH'
     A                                      COLOR(BLU)
     A          R FOOTER_ADD                OVERLAY
     A                                 23 10'F3-EXIT'
     A                                      COLOR(BLU)
     A                                 23 26'F5-REFRESH'
     A                                      COLOR(BLU)
     A                                 23 45'F8-ADD'
     A                                 23 61'F12-CANCEL'
     A                                      COLOR(BLU)
     A          R FOOTER_UP                 OVERLAY
     A                                 23  6'F3-EXIT'
     A                                      COLOR(BLU)
     A                                 23 19'F5-REFRESH'
     A                                      COLOR(BLU)
     A                                 23 35'F8-UPDATE'
     A                                 23 50'F11-FOLD/DROP'
     A                                 23 68'F12-CANCEL'
     A                                      COLOR(BLU)
     A          R LOGIN                     CF05(05)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                  5 28'Welcome:'
     A                                  5 37USER
     A                                  9 22'User Name...:'
     A                                 10 22'Password....:'
     A            MSG_LOGIN     50   O 14 16COLOR(PNK)
     A            USRNAME       10   B  9 36CHECK(LC)
     A            USRPWD        10   B 10 36CHECK(LC)
     A                                      DSPATR(ND)
     A                                  7 25'Please log in to continue'
     A          R MAINMENU                  CF05(05)
     A                                      OVERLAY
     A                                  5  8'1-Select'
     A                                  7 23'Add New Subject'
     A                                  9 23'Add New Course'
     A                                 11 23'Add New Student'
     A                                 13 23'View/Add Student Details'
     A            OPT_NEWSUB     1M  I  7 21
     A            OPT_NEWCRS     1M  I  9 21
     A            OPT_NEWSTU     1M  I 11 21
     A            OPT_VIEWST     1M  I 13 21
     A            MSG_MENU      50   O 16 15COLOR(PNK)
     A          R NEWSUBJECT                CF08(08)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                  5  6'Add New Subject'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  9 20'Subject Name:'
     A            NEWSUBNAME    25   B  9 34CHECK(LC)
     A            MSG_NEWSUB    50   O 13 17COLOR(PNK)
     A          R MSGREC                    OVERLAY
     A            MESSAGE       60   O 22 10COLOR(TRQ)
     A          R SUBSFL                    SFL
     A            SUB_ID         5N 0O 13 25
     A            SUB_NAME      25   O 13 43
     A            OPT_SUB        1M  I 13  9
     A          R SUBCTL                    SFLCTL(SUBSFL)
     A  50                                  SFLDSPCTL
     A  50                                  SFLDSP
     A N50                                  SFLCLR
     A  50                                  SFLEND(*MORE)
     A                                      OVERLAY
     A                                      SFLPAG(6)
     A                                      SFLSIZ(1000)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      CF08(08)
     A                                 12  8'OPT'
     A                                 12 23'Subject ID'
     A                                 12 49'Subject Name'
     A                                  4  4'Add New Course'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  6 23'Course....:'
     A            CRSNAME       25   B  6 35CHECK(LC)
     A                                  8 23'Semester..:'
     A            SEMESTER      10   B  8 35CHECK(LC)
     A                                 11  5'1-Select'
     A            MSG_NEWCRS    70   O 10  6COLOR(PNK)
     A          R CRSSFL                    SFL
     A  60                                  SFLNXTCHG
     A            OPT_CRS        1M  I 13  9
     A            CRS_NAME      25   O 13 43
     A            CRS_ID         5N 0O 13 25
     A          R CRSCTL                    SFLCTL(CRSSFL)
     A  55                                  SFLDSPCTL
     A  55                                  SFLDSP
     A N55                                  SFLCLR
     A  55                                  SFLEND(*MORE)
     A                                      OVERLAY
     A                                      SFLPAG(6)
     A                                      SFLSIZ(1000)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      CF08(08)
     A                                 12  8'OPT'
     A                                 12 23'Course ID'
     A                                 12 50'Course Name'
     A                                  4  4'Add New Student'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  6 21'Student Name...:'
     A            NSTUDNAME     25   B  6 39CHECK(LC)
     A                                  8 18'Student Address:..:'
     A            NSTUDADDR     25   B  8 39CHECK(LC)
     A                                 11  5'1-Select'
     A            MSG_NEWSTU    70   O 10  6COLOR(PNK)
     A          R STUDSFL                   SFL
     A  60                                  SFLNXTCHG
     A            OPT_STUD       1   B 11  2
     A            ST_ID          5N 0O 11  4
     A            ST_NAME       25   O 11 12
     A            ST_CRSID       5N 0O 11 38
     A            ST_SEMEST     10   O 11 45
     A            ST_SUBID       5N 0O 11 60
     A            ST_MARKS       5N 0B 11 71
     A            ST_ADDR       25   O 13  4
     A                                 12  4'Address'
     A                                      COLOR(WHT)
     A                                 12 30'Subject'
     A                                      COLOR(WHT)
     A                                 12 56'Course Name'
     A                                      COLOR(WHT)
     A            ST_SUBJECT    25   O 13 30
     A            ST_CRSNAME    25   O 13 56
     A                                 14  4'-----------------------------------
     A                                      ------------------------------------
     A                                      --'
     A                                      COLOR(WHT)
     A          R STUDCTL                   SFLCTL(STUDSFL)
     A  50                                  SFLDSPCTL
     A  51                                  SFLDSP
     A N50                                  SFLCLR
     A  50                                  SFLEND(*MORE)
     A                                      OVERLAY
     A                                      SFLPAG(2)
     A                                      SFLSIZ(1000)
     A                                      CF12(12)
     A                                      CF05(05)
     A                                      CF08(08)
     A  31                                  SFLDROP(CF11)
     A N31                                  SFLFOLD(CF11)
     A                                      SFLMODE(&MODE)
     A                                      CF04(04)
     A            MODE           1A  H
     A                                 10  1'Opt'
     A                                      COLOR(WHT)
     A                                 10  5'ID'
     A                                      COLOR(WHT)
     A                                 10 12'Student Name'
     A                                      COLOR(WHT)
     A                                 10 37'Crs ID'
     A                                      COLOR(WHT)
     A                                 10 46'Semester'
     A                                      COLOR(WHT)
     A                                 10 57'Subject ID'
     A                                      COLOR(WHT)
     A                                 10 71'Marks'
     A                                      COLOR(WHT)
     A                                  3  7'Student ID:'
     A            ST_INPID       5N 0B  3 19
     A                                  4  7'Course ID:'
     A            ST_INPCRS      5N 0B  4 19
     A                                  5  7'Semester:'
     A            ST_INPSEM     10   B  5 19CHECK(LC)
     A                                  3 33'OR'
     A                                  4 33'OR'
     A                                  3 38'Student Name:'
     A            ST_INPNAM     25   B  3 52CHECK(LC)
     A                                  4 38'Course Name:'
     A            ST_INPCRSN    25   B  4 52CHECK(LC)
     A                                  9  2'A-Add New Subject'
     A                                      COLOR(BLU)
     A                                  9 21'C-Change Semester'
     A                                      COLOR(BLU)
     A                                  9 42'D-Delete'
     A                                      COLOR(BLU)
     A            MSG_STUDET    70   O  7  6COLOR(PNK)
     A          R ADDDETAIL                 OVERLAY
     A                                      CF12(12)
     A                                      CF08(08)
     A                                      CF05(05)
     A                                      CF04(04)
     A                                  4  6'Add Details'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  6 19'Student ID....:'
     A                                  8 19'Student Name..:'
     A                                 10 19'Course........:'
     A                                 12 19'Semester......:'
     A                                 14 19'Subject.......:'
     A                                 16 19'Marks.........:'
     A            DT_STID        5N 0O  6 37
     A            DT_STNAME     25   O  8 37
     A            DT_CRSNAME    25   O 10 37
     A            DT_SEMEST     10   O 12 37
     A            DT_SUBNAME    25   B 14 37CHECK(LC)
     A            DT_MARKS       5N 0B 16 37
     A            MSG_ADDDET    70   O 19  4COLOR(PNK)
     A                                 22 20'F4 for a list of available subject-
     A                                      s'
     A                                      COLOR(WHT)
     A          R CHGSMST                   OVERLAY
     A                                      CF12(12)
     A                                      CF08(08)
     A                                      CF05(05)
     A                                  4  6'Change Semester'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  6 19'Student ID....:'
     A                                  8 19'Student Name..:'
     A                                 10 19'Course........:'
     A            CS_CRSNAM     25     10 36
     A            CS_STNAME     25      8 36
     A            CS_STID        5N 0   6 36
     A                                 12 19'Semester......:'
     A            CS_SEMEST     10   B 12 36CHECK(LC)
     A            MSG_CHGSEM    70   O 19  4COLOR(PNK)
     A          R NORESULT                  OVERLAY
     A N51                             12 33'No Results found'
     A                                      COLOR(RED)
     A          R WIN_OPT                   WINDOW(6 19 8 38)
     A                                      WDWTITLE((*TEXT 'Choose an option')-
     A                                       (*COLOR WHT) *TOP *CENTER)
     A                                      WDWBORDER((*COLOR WHT))
     A                                      WDWBORDER((*CHAR '...::...'))
     A            STU_SEL        2Y 0B  5  8SNGCHCFLD
                                            CHOICE(1 '>A - Add Subject     ')
                                            CHOICE(2 '>C - Change Semeste  ')
                                            CHOICE(3 '>D - Delete Entry    ')
     A                                  2  9'Use [Space] to select'
     A                                      COLOR(WHT)
     A                                  3 13'[Enter] to return '
     A                                      COLOR(WHT)
     A          R WIN_DEL                   CF12(12)
     A                                      WINDOW(5 14 14 50)
     A                                      WDWTITLE((*TEXT 'Confirmation') +
     A                                      (*COLOR WHT) *TOP *CENTER)
     A                                      WDWBORDER((*COLOR WHT))
     A                                  8 19'Are you sure?'
     A                                  4 15' This cannot be undone. '
     A                                      COLOR(RED)
     A                                      DSPATR(HI RI)
     A                                  2  5'[Space] to select'
     A                                      COLOR(WHT)
     A                                  2 29'[Enter] to confirm'
     A                                      COLOR(WHT)
     A            DEL_SEL        2Y 0B 11 15SNGCHCFLD
     A                                      CHOICE(1 '>YES')
     A                                      CHOICE(2 '>NO')
     A                                      CHOICE(3 '>CANCEL')
     A                                  6 36'StuID:'
     A            DEL_STUID      5N 0O  6 43
     A                                  6 12'Subject ID:'
     A            DEL_SUBID      5N 0O  6 24COLOR(RED)
     A                                  6  3'Removing'
     A                                  6 31'from'
     A          R WIN_DELRM                 WINDOW(5 14 14 50)
     A                                      WDWTITLE((*TEXT 'Confirmation') +
     A                                      (*COLOR WHT) *TOP *CENTER)
     A                                      WDWBORDER((*COLOR WHT))
     A                                      CF12(12)
     A                                  2  5'[Space] to select'
     A                                      COLOR(WHT)
     A                                  2 29'[Enter] to confirm'
     A                                      COLOR(WHT)
     A                                  4 15' This cannot be undone. '
     A                                      COLOR(RED)
     A                                      DSPATR(HI RI)
     A                                  6  4'** This is the last subject on thi-
     A                                      s student **'
     A                                      DSPATR(UL)
     A                                  9  7'Do you want to:'
     A            DELRM_SEL      2Y 0B 11 14SNGCHCFLD
     A                                      CHOICE(1 '>REMOVE last subject')
     A                                      CHOICE(2 '>DELETE student')
     A                                      CHOICE(3 '>CANCEL')
     A                                  7 16'Student ID:'
     A            DELRM_STID     5N 0O  7 28COLOR(RED)
     A          R AVASUBSFL                 SFL
     A            AV_SUBID       5N 0O 12 18
     A            AV_SUBNAME    25     12 37
     A          R AVASUBCTL                 SFLCTL(AVASUBSFL)
     A                                      OVERLAY
     A  70                                  SFLDSPCTL
     A  71                                  SFLDSP
     A N70                                  SFLCLR
     A  70                                  SFLEND(*MORE)
     A                                      SFLPAG(6)
     A                                      SFLSIZ(1000)
     A                                      CF12(12)
     A                                  4  6'Available Subjects for:'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  6 11'Course:'
     A                                  7 11'Course Semester:'
     A            AV_CRSNAME    25      6 28
     A            AV_SEMEST     10      7 28
     A                                 11 16'Subject ID'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 11 43'Subject Name'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A            MSG_AV        50   O  9 11COLOR(PNK)
     A          R FOOTER_BAC                OVERLAY
     A                                 23 23'F3-Exit'
     A                                      COLOR(BLU)
     A                                 23 52'F12-BACK'
     A                                      COLOR(BLU)
     A          R NOAVASUB                  OVERLAY
     A                                 14 29'No available subjects'
     A                                      COLOR(RED)
     A                                 16 19'Consider adding a few subjects in -
     A                                      Add Course'
     A                                 17 19'for the'
     A                                 17 27'corresponding semester'
     A                                      DSPATR(UL)
     A*%%RS+<record-sequences>
     A*%%RS+ <sequence name="Untitled">
     A*%%RS+  <device type="display" width="80" height="24" />
     A*%%RS+ </sequence>
     A*%%RS </record-sequences>
